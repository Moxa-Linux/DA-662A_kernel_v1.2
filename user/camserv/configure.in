AC_INIT(camserv/camserv.c)
CAMSERV_VER_MAJOR=0
CAMSERV_VER_MINOR=5
CAMSERV_VER_BUGFIX=1
AC_DEFINE_UNQUOTED(CAMSERV_VER_MAJOR,$CAMSERV_VER_MAJOR)
AC_DEFINE_UNQUOTED(CAMSERV_VER_MINOR,$CAMSERV_VER_MINOR)
AC_DEFINE_UNQUOTED(CAMSERV_VER_BUGFIX,$CAMSERV_VER_BUGFIX)
AM_INIT_AUTOMAKE(camserv,$CAMSERV_VER_MAJOR.$CAMSERV_VER_MINOR.$CAMSERV_VER_BUGFIX)
AM_CONFIG_HEADER(include/camserv_config.h:config.in)
AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

AC_PROG_CC
AC_LIBLTDL_CONVENIENCE
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_SUBST(LIBLTDL)

AC_CONFIG_SUBDIRS(libltdl)

AC_CHECK_LIB(m,sqrt)
AC_CHECK_LIB(jpeg,jpeg_start_compress)
AC_CHECK_LIB(socket,socket)
AC_CHECK_LIB(nsl,inet_ntoa)

have_imlib2=no
AC_PATH_PROG(IMLIB2_CONFIG,imlib2-config)
if test "$IMLIB2_CONFIG"x != "x"; then
	IMLIB2_VERSION=`$IMLIB2_CONFIG --version`
	case "$IMLIB2_VERSION" in
	    1.*) IMLIB2_CFLAGS=`$IMLIB2_CONFIG --cflags`
		 IMLIB2_LIBS=`$IMLIB2_CONFIG --libs`
		 AC_CHECK_HEADER(Imlib2.h,have_imlib2=yes)
		 CFLAGS="$CFLAGS $IMLIB2_CFLAGS"
		 ;;
	esac
fi	

AC_SUBST(IMLIB2_CFLAGS)
AC_SUBST(IMLIB2_LIBS)

AM_CONDITIONAL(HAVE_IMLIB2, test $have_imlib2 = yes)
if test $have_imlib2 = no ; then
	echo 
	echo "Imlib2 is either not installed on this system, or is too old"
        echo "You will not be able to use the Imlib2 filter.  To obtain the"
	echo "newest version, visit: http://www.rasterman.com/imlib.html"
	echo
fi


have_gdkpixbuf=no
AC_PATH_PROG(GDKPIXBUF_CONFIG,gdk-pixbuf-config)
if test "$GDKPIXBUF_CONFIG"x != "x"; then
	GDKPIXBUF_VERSION=`$GDKPIXBUF_CONFIG --version`
	case "$GDKPIXBUF_VERSION" in
	    0.14.*) have_gdkpixbuf=yes
		 GDKPIXBUF_CFLAGS=`$GDKPIXBUF_CONFIG --cflags`
		 GDKPIXBUF_LIBS=`$GDKPIXBUF_CONFIG --libs`
		 CFLAGS="$CFLAGS $GDKPIXBUF_CFLAGS"
		 ;;
	esac
fi	

AC_SUBST(GDKPIXBUF_CFLAGS)
AC_SUBST(GDKPIXBUF_LIBS)

AM_CONDITIONAL(HAVE_GDKPIXBUF, test $have_gdkpixbuf = yes)
if test $have_gdkpixbuf = no ; then
	echo 
	echo "Gdkpixbuf is either not installed on this system, or is too old"
        echo "You will not be able to use the Gdkpixbuf filter.  To obtain the"
	echo "newest version, visit: http://www.gnome.org"
	echo
fi

CAMSERV_JPEG_VALID
AM_CONDITIONAL(JPEG_VALID, test $av_jpeg_valid = yes)

AC_HEADER_STDC
AC_C_CONST
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/time.h)

AC_FUNC_MMAP
AC_TYPE_SIGNAL

AC_PATH_XTRA

dnl Check to see if we have any valid methods for video
AC_CHECK_HEADERS(linux/videodev.h,
[video_v4l=yes],[video_v4l=no])
AM_CONDITIONAL(VIDEO_V4L, test $video_v4l = yes)

AC_CHECK_HEADERS(machine/ioctl_bt848.h,
[video_fbsd_bttv=yes],[video_fbsd_bttv=no])
AM_CONDITIONAL(VIDEO_FBSD_BTTV, test $video_fbsd_bttv = yes)

if test $av_jpeg_valid = yes ; then
	valid_video_inputs="video_basic"
else
	echo 
	echo "A valid JPEG library was not found.  Most stuff will not work."
	echo
	valid_video_inputs=""
fi

if test $video_fbsd_bttv = yes ; then
    if test $av_jpeg_valid = no ; then
	echo 
	echo "FreeBSD bttv header was detected, but your JPEG library is "
	echo "too old.  Please upgrade to at least libjpeg version 6b"
	echo "Neither the video library, nor the jpeg filter will be built"
	echo
    else
	valid_video_inputs="$valid_video_inputs fbsd_bttv"
    fi
fi

if test $video_v4l = yes ; then
    if test $av_jpeg_valid = no ; then
	echo
        echo "Video4Linux was detected, but your JPEG library is too old"
	echo "Please upgrade to at least libjpeg version 6b"
	echo "Neither the video library, nor the jpeg filter will be built"
	echo
    else
	valid_video_inputs="$valid_video_inputs v4l"
    fi	
fi

if test "_$valid_video_inputs" = _ ; then
    echo
    echo "ALERT:  No valid video devices were found to compile for."
    echo "The camerv binary will NOT be compiled, however the relay "
    echo "binary will be built."
    echo
else
    echo
    echo "Building for video modules: $valid_video_inputs"
    echo
fi

AM_CONDITIONAL(VALID_VIDEO, test "$valid_video_inputs" != "" )

if test "$CC" = "gcc" ; then
	CPPFLAGS="$CPPFLAGS -Wall"
fi	

CAMSERV_MODDIR=`echo $libdir/camserv`
AC_SUBST(CAMSERV_MODDIR)

AC_OUTPUT(
	Makefile
	camserv.spec
	camserv/Makefile 
	common/Makefile 
	include/Makefile
	macros/Makefile 
	relay/Makefile	
)

